# Copyright (C) 2017 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//ipc//ipc_library.gni")

# tracing service API surface.
ipc_library("tracing_service") {
  sources = [
    "consumer_port.proto",
    "data_source_config.proto",
    "data_source_descriptor.proto",
    "producer_port.proto",
    "trace_config.proto",
  ]
}

full_protos_base_dir3 = "${root_gen_dir}/protos_full"
full_protos_target_dir3 =
    "$full_protos_base_dir3/" + rebase_path(target_gen_dir, root_gen_dir)

proto_names3 = [
  "data_source_config.proto",
  "data_source_descriptor.proto",
  "trace_config.proto",
]

action_foreach("generate_full_protos") {
  visibility = [ ":*" ]
  script = "//build/generate_full_protos.py"
  sources = proto_names3

  output_directory = full_protos_target_dir3
  outputs = [
    "$output_directory/{{source_name_part}}.proto",
  ]

  args = [
    "{{source}}",
    rebase_path(output_directory, root_build_dir) +
        "/{{source_name_part}}.proto",
  ]
}

proto_library("full") {
  deps = [
    ":generate_full_protos",
  ]
  sources = []
  use_protobuf_full = true
  proto_out_dir = "protos_full"
  proto_in_dir = full_protos_base_dir3
  foreach(proto, proto_names3) {
    sources += [ "$full_protos_target_dir3/$proto" ]
  }
}
