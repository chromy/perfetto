# Copyright (C) 2017 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

executable("perfetto") {
  deps += [
    ":consumer",
    ":producer",
    ":service",
    "//base",
  ]
  sources = [
    "main.cc",
  ]
}

source_set("service") {
  deps += [
    ":common",
    "//base",
    "//tracing",
    "//tracing:ipc",
  ]
  sources = [
    "service.cc",
  ]
}

source_set("producer") {
  deps += [
    ":common",
    "//base",
    "//ftrace_reader",
    "//protos:zero",
    "//tracing",
    "//tracing:ipc",
  ]
  sources = [
    "producer.cc",
  ]
}

source_set("consumer") {
  deps += [
    ":common",
    "//base",
    "//protos:lite",
    "//tracing",
    "//tracing:ipc",
  ]
  sources = [
    "consumer.cc",
  ]
}

source_set("common") {
  deps += [ "//base" ]
  sources = [
    "common.cc",
  ]
}

if (current_toolchain == host_toolchain) {
  executable("proto_to_text_host") {
    deps += [
      "//base",
      "//protos:full($current_toolchain)",
    ]
    sources = [
      "proto_to_text.cc",
    ]
  }

  copy("proto_to_text") {
    ptt_out_dir = get_label_info(":proto_to_text_host", "root_out_dir")
    deps = [
      ":proto_to_text_host",
    ]
    sources = [
      "${ptt_out_dir}/proto_to_text_host",
    ]
    outputs = [
      "${root_build_dir}/proto_to_text",
    ]
  }

  executable("gen_trace_config_host") {
    deps += [
      "//base",
      "//protos/ipc:full",
    ]
    sources = [
      "gen_trace_config.cc",
    ]
  }

  copy("gen_trace_config") {
    ptt_out_dir = get_label_info(":gen_trace_config_host", "root_out_dir")
    deps = [
      ":gen_trace_config_host",
    ]
    sources = [
      "${ptt_out_dir}/gen_trace_config_host",
    ]
    outputs = [
      "${root_build_dir}/gen_trace_config",
    ]
  }
}  # host_toolchain
