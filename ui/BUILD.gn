# Copyright (C) 2018 The Android Open Source Project
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../gn/perfetto.gni")

root_output_dir = "ui"

template("protos_json") {
  assert(defined(invoker.name))
  assert(defined(invoker.sources))
  invoker_name = invoker.name
  invoker_sources = invoker.sources
  output_path = "${target_out_dir}/${invoker_name}"

  action(target_name) {
    sources = invoker_sources

    inputs = ["${perfetto_root_path}package-lock.json"]
    script = "//gn/standalone/build_tool_wrapper.py"
    pbjs_path = "${perfetto_root_path}node_modules/protobufjs/bin/pbjs"
    args = [ rebase_path(pbjs_path, "") ]
    args += rebase_path(sources, "")
    args += ["-o", rebase_path(output_path, "")]
    outputs = [output_path]
  }
}

template("js") {
  assert(defined(invoker.sources))
  invoker_sources = invoker.sources
  
  copy(target_name) {
    sources = invoker_sources
    outputs = [ "$target_out_dir/{{source_file_part}}" ]
  }
}

template("js_bundle") {
  assert(defined(invoker.name))
  assert(defined(invoker.deps))
  invoker_name = invoker.name
  invoker_deps = invoker.deps

  all_sources = [] 
  foreach(dep, invoker_deps) {
    foreach(source, get_target_outputs(dep)) {
      all_sources += [source]
    }  
  }
  output_path = "${root_out_dir}/${invoker_name}"

  action(target_name) {
    deps = invoker_deps
    inputs = ["${perfetto_root_path}package-lock.json"]
    script = "//gn/standalone/build_tool_wrapper.py"
    sources = all_sources
    browserify_path = "${perfetto_root_path}node_modules/browserify/bin/cmd.js"
    args = [
      rebase_path(browserify_path, ""),
    ] + rebase_path(all_sources, "") + [
      "-o",
      rebase_path(output_path, "")
    ]
    outputs = [output_path]
  }
}

group("ui") {
  deps = [
    ":index",
    ":css",
    ":js",
  ]
}

copy("index") {
  sources = ["perfetto.html"]
  outputs = ["$root_out_dir/ui/index.html"]
}

copy("css") {
  sources = ["perfetto.css"]
  outputs = ["$root_out_dir/ui/perfetto.css"]
}

protos_json("config_proto_json") {
  name = "perfetto_config.json"
  sources = [
    "../protos/perfetto/config/perfetto_config.proto",
  ]
}

protos_json("trace_proto_json") {
  name = "perfetto_trace.json"
  sources = [
    "../protos/perfetto/trace/perfetto_trace.proto",
  ]
}

js("js") {
  sources = [
    "main.js",
    "api.js",
  ]
}

js_bundle("bundle") {
  deps = [
    ":js",
    ":trace_proto_json",
    ":config_proto_json",
  ]
  name = "ui/perfetto.js"
}


